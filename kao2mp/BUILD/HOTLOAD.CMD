@ECHO OFF

ECHO ****************
ECHO * HOTLOAD.EXE  *
ECHO ****************

:: Assert output directories exist

IF NOT EXIST OUT\ MD OUT
IF NOT EXIST OUT\OBJ\ MD OUT\OBJ
IF NOT EXIST OUT\OBJ\HOTLOAD MD OUT\OBJ\HOTLOAD

:: Compile "HOTLOAD\HOTLOAD.C"

CL.EXE /nologo ^
    /W4 /WX /GS- /Oi %_COMPILER_FLAGS% ^
    ^
    /I"SOURCE" ^
    /c /TC "SOURCE\HOTLOAD\HOTLOAD.C" ^
    /Fo:"OUT\OBJ\HOTLOAD\HOTLOAD.OBJ"

IF ERRORLEVEL 1 EXIT /B 1

:: Translate "HOTLOAD.RC"

RC.EXE /nologo ^
    /fo "OUT\OBJ\HOTLOAD\HOTLOAD.RES" ^
    "RESOURCE\HOTLOAD.RC"

IF ERRORLEVEL 1 EXIT /B 1

:: Link "HOTLOAD.EXE"

LINK.EXE /NOLOGO ^
    /EMITTOOLVERSIONINFO:NO /EMITPOGOPHASEINFO /MANIFEST:NO ^
    /STUB:"OUT\STUBS\MZ16STUB.EXE" ^
    /SUBSYSTEM:CONSOLE %_LINKER_FLAGS% ^
    /NODEFAULTLIB /ENTRY:EntryPoint ^
    ^
    "KERNEL32.LIB" "SHELL32.LIB" "SHLWAPI.LIB" ^
    ^
    "OUT\OBJ\HOTLOAD\HOTLOAD.OBJ" ^
    "OUT\OBJ\HOTLOAD\HOTLOAD.RES" ^
    ^
    /OUT:"OUT\HOTLOAD.EXE"

IF ERRORLEVEL 1 EXIT /B 1

EXIT /B 0
